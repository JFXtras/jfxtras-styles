plugins {
    id "com.jfrog.bintray" version "1.7.3"
    id "java-library"
    id "maven-publish"
}

sourceCompatibility = '10'

version = '4.4'
def versionDescription = 'JMetro'


[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

dependencies {
    implementation 'org.controlsfx:controlsfx:9.0.0' // jfxtras.styles.jmetro8
    implementation 'org.jfxtras:jfxtras-labs:2.2-r5' // jfxtras.styles.jmetro
}

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
}

jar {
    manifest {
        attributes( 'Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

javadoc {
    options.addStringOption('javafx', '-quiet')
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

artifacts {
    archives sourceJar
    archives javadocJar
}

/* Publishing */
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = project.name
        //userOrg = 'dukke'
        licenses = ['BSD New']
        vcsUrl = 'https://github.com/JFXtras/jfxtras-styles.git'
        publications = ['mavenJava']
        websiteUrl = 'https://www.pixelduke.com/jmetro'
        issueTrackerUrl = 'https://github.com/JFXtras/jfxtras-styles/issues'
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = versionDescription // Version description
            vcsTag = project.version
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}




