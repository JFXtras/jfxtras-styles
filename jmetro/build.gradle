plugins {
    id "com.jfrog.bintray" version "1.7.3"
    id "java-library"
    id "maven-publish"
    id "eclipse"
}

sourceCompatibility = '1.9'

version = '4.5'
def versionDescription = 'JMetro'

dependencies {
    implementation 'org.controlsfx:controlsfx:9.0.0' // jfxtras.styles.jmetro8
    // implementation 'org.jfxtras:jfxtras-labs:2.2-r5' // jfxtras.styles.jmetro
}

ext.moduleName = 'org.jfxtras.jmetro'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

compileJava {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.compilerArgs = [
            '--module-path', classpath.asPath
        ]
        classpath = files()
    }
}
// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
}

jar {
    inputs.property("moduleName", moduleName)
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

javadoc {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.addStringOption('javafx', '-quiet')
        options.addStringOption('-module-path', classpath.asPath)
        options.addBooleanOption('html5', true)
    }
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

artifacts {
    archives sourceJar
    archives javadocJar
}

/* Publishing */
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = project.name
        //userOrg = 'dukke'
        licenses = ['BSD New']
        vcsUrl = 'https://github.com/JFXtras/jfxtras-styles.git'
        publications = ['mavenJava']
        websiteUrl = 'https://www.pixelduke.com/jmetro'
        issueTrackerUrl = 'https://github.com/JFXtras/jfxtras-styles/issues'
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = versionDescription // Version description
            vcsTag = project.version
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
        }
    }
}

eclipse.classpath.file {
    whenMerged {
        entries.findAll { 
                it.kind == 'src' || it.kind == 'lib' 
        }.each { it.entryAttributes['module'] = 'true' }
    }
}

